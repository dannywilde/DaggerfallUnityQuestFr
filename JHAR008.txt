Quest: JHAR008
DisplayName: Preuve Perdue
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>	Notre association attire toutes sortes de personnes,
<ce>	et tous ne méritent pas notre confiance. De temps en temps nous
<ce>	faisons des erreurs. J'ai besoin de quelqu'un pour récupérer _tri_
<ce>	un de nos membres, ou anciens membres,
<ce>	s'est enfui avec. Allez-vous punir le coupable
<ce>	et récupérer ce qui a été volé

RefuseQuest:  [1001]
<ce>	Comment vous faire confiance ?
<--->
<ce>	Nous avons besoin de votre aide pour autre chose, éventuellement.

AcceptQuest:  [1002]
<ce>	Pour être clair, nous ne savons pas où _dummy_ est allé.
<ce>	Vous devrez faire à la fois le détective et les démarches pour le récupérer
<ce>	J'espère que notre récompense de =reward_ pièces d'or
<ce>	sera suffisante pour l'effort. Veuillez apporter le
<ce>	le _tri_ ici d'ici' =timer_ jours. Bonne chance.

QuestComplete:  [1004]
<ce>	Excellent travail. Ne vous préoccupez pas de la vie de
<ce>	notre ami. Tôt ou tard, la même chose se serait produite par
<ce>	la main de quelqu'un d'autre. Personne ne lèvera un sourcil.
<ce>	Recevez vos honoraires et mes remerciements.

RumorsDuringQuest:  [1005]
Ils disent qu'il y a un voleur d'artefacts à ___rand_.
<--->
J'ai entendu dire que les Archéologues étaient furieux à propos d'un voleur à ___rand_.

Message:  1020
Je suis parti pour ___rand_, fuyant les Archéologues.
<--->
___rand_. C'est tout ce que je sais.

Message:  1025
<ce>	Vous assassinez le voleur.
<ce>	Vous pouvez désormais récupérer le _tri_.

Message:  1030
%qdt:
 _qgiver_ m'a demandé de
 récupérer un _tri_ d'un
 ancien membre des archéologues
 et le remettre à ___qgiver_
 d'ici =timer_ jours.

QBN:
Person _qgiver_ group Questor male
Person _dummy_ group Local_3.0 anyInfo 1020 rumors 1020

Place _rand_ remote random

Foe _enemy_ is Thief

Clock _timer_ 00:00 0 flag 17 range 0 2

Item _tri_ trinket
Item _reward_ gold range 250 to 400

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0 
	place foe _enemy_ at _rand_
    place npc _dummy_ at _rand_
	give item _tri_ to _enemy_
	pc at _rand_ do _hide_

_hide_ task:
    hide npc _dummy_

_slain_ task:
	killed 1 _enemy_ saying 1025

_victory_ task:
	toting _tri_ and _qgiver_ clicked 
	give pc _reward_
	end quest 

_qgclicked_ task:
	clicked npc _qgiver_ 

_timer_ task:
	end quest
