-- Quest: C:\QUESTS\WORKING\J0B00Y00.Qbn.
-- StartsBy: NPC
-- Questee: member
-- Questor: Zenithar
-- Repute: 0
-- QuestId: 0
Messages: 31
Quest: J0B00Y00
DisplayName: The Bounty
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                Si vous êtes disponible pour une mission, il
<ce>                se pourrait que j'aie un travail pour vous. C'est une
<ce>                affaire un peu compromettante, qui implique de punir
<ce>                un voleur. Il y aura _reward_ pièces d'or en récompense.
<ce>                Vous pensez que cela pourrait vous intéresser ?

RefuseQuest:  [1001]
<ce>               Décevant. Bien, nous trouverons
<ce>               quelqu'un d'autre, sans aucun doute.

AcceptQuest:  [1002]
<ce>                Il est rare que nous nous fassions justice dans
<ce>                nos affaires, mais dans le cas présent, nous
<ce>                pensons que c'est justifié. Ce voleur, voyez-vous, a
<ce>                volé directement dans l'une de nos caravanes et
<ce>                nous rançonne pour l'utilisation de la grand route.
<ce>                Très énervant. Vous trouverez le petit escroc
<ce>                dans une cachette appelée ___mondung_.
<ce>                Soyez de retour dans =1stparton_ jours.

QuestFail:  [1003]
<ce>                   Vous feriez mieux de vous remuer, %pcf.
<ce>                   Vous n'avez que =1stparton_ jours
<ce>                   pour tuer cet escroc.

QuestComplete:  [1004]
<ce>                        Très bon travail, %pcf.
<ce>                        Cela devrait décourager d'autres
<ce>                        bandits. Voici vos _reward_
<ce>                        pièces d'or. Merci.

RumorsDuringQuest:  [1005]
A lui tout seul, ce bandit est en train de ruiner l'économie.
<--->
<ce>         Ils pensent que ce bandit de grands chemins est un voleur,
<ce>          mais le malfaiteur est très habile avec une épée.

RumorsPostfailure:  [1006]
Ce bandit est parti vers une autre cachette, mais les routes ne sont toujours pas sûres.
<--->
Je ne comprends pas pourquoi ils ne parviennent pas a arrêter ce bandit de grands chemins. Ca devrait être facile.

RumorsPostsuccess:  [1007]
Les marchants sont plutôt contents d'avoir finalement attrapé ce voleur.
<--->
Je me fiche qu'ils aient attrapé ce criminel. Les routes ne sont pas plus sûres pour autant.

QuestorPostsuccess:  [1008]
Bien sûr que je vais discuter avec vous, %pcf. Je me rappelle ces bandits que vous avez tués.

QuestorPostfailure:  [1009]
La Résolution continue de se faire dérober son or par ces bandits de grands chemins. Pas très amusant.

Message:  1020
<ce>                   Vous devez être envoyé par la Résolution.
<ce>                   Préparez-vous à mourir, chasseur de prime !

Message:  1030
%qdt:
La Résolution de Zenithar
m'a envoyé tuer un voleur qui a rançonné
leurs caravanes. ___mondung_ est
le repaire de cet escroc. Je dois
tuer le voleur et être de retour
à ___questgiver_ dans =1stparton_ jours.


-- Symbols used in the QRC file:
--
--              %pcf occurs 3 times.
--              %qdt occurs 1 time.
--       =1stparton_ occurs 6 times.
--       ___mondung_ occurs 2 times.
--    ___questgiver_ occurs 1 time.
--          _reward_ occurs 4 times.

QBN:
Item _reward_ gold

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon

Clock _1stparton_ 00:00 0 flag 17 range 0 2

Foe _monster1_ is Nightblade
Foe _monster2_ is Assassin
Foe _monster3_ is Thief
Foe _monster4_ is Spellsword

--	Quest start-up:
	start timer _1stparton_
	reveal _mondung_
	log 1030 step 0
	pick one of _S.05_ _S.06_ _S.07_ _S.08_

_pcgetsgold_ task:
	when _qgclicked_ and _mondead_
	give pc _reward_
	end quest

_mondead_ task:
	killed 1 _monster4_
	killed 1 _monster2_
	killed 1 _monster1_
	killed 1 _monster3_

_qgclicked_ task:
	clicked npc _questgiver_

_1stparton_ task:
	end quest

_clearclick_ task:
	when _qgclicked_ and not _mondead_
	say 1003
	clear _qgclicked_ _clearclick_

_S.05_ task:
	place foe _monster1_ at _mondung_

_S.06_ task:
	place foe _monster2_ at _mondung_

_S.07_ task:
	place foe _monster3_ at _mondung_

_S.08_ task:
	place foe _monster4_ at _mondung_

_S.09_ task:
	injured _monster4_ saying 1020
	injured _monster3_ saying 1020
	injured _monster2_ saying 1020
	injured _monster1_ saying 1020
