-- Quest: C:\QUESTS\WORKING\C0B00Y00.Qbn.
-- StartsBy: NPC
-- Questee: member
-- Questor: temple
-- Repute: 0
-- QuestId: 0
-- Edited for Daggerfall Unity by Jay_H
Messages: 13
Quest: C0B00Y00
DisplayName: The Haunted House
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                  Une maison quelque part dans __house_
<ce>            est actuellement hantée par une sorte de mort-vivant.
<ce>            Nous avons besoin de quelqu'un pour exorciser cet esprit.
<ce>                         En êtes-vous capable ?
<ce>                Cela vous rapportera _gold_ pièces d'or.

RefuseQuest:  [1001]
<ce>         Il n'y a pas de honte à cela. Je garderai un oeil sur les
<ce>                emplois qui vous conviennent mieux.

AcceptQuest:  [1002]
<ce>           Rappelez-vous, si la créature est trop forte pour que
<ce>           vous vous en occupiez seul, contentez-vous de fuir.
<ce>           Personne ne vous reprochera d'avoir sauvé votre peau.
<ce>           L'endroit s'appelle _house_ et se situe dans la région
<ce>                     de __house_.
<ce>           Vous avez =traveltime_ jours ou nous enverrons
<ce>                quelqu'un d'autre faire le travail.

QuestComplete:  [1004]
<ce>         Alors, la maison n'est plus hantée. Je vais brûler de l'encens
<ce>         sur l'autel pour cet esprit. Voilà les gages du temple.

RumorsDuringQuest:  [1005]
Le fantôme de _house_ ne peut être tué, selon eux.

RumorsPostfailure:  [1006]
Le fantôme de _house_ reviendra sans cesse.
<--->
Les esprits de _house_ ne sont toujours pas en paix.

RumorsPostsuccess:  [1007]
_house_ est enfin nettoyée.
<--->
Les esprits de _house_ sont enfin en paix.

QuestorPostsuccess:  [1008]
Comment puis-je vous aider, ô exorciste de _house_?

QuestorPostfailure:  [1009]
_house_ est toujours hantée, et je devrais parler avec vous ?

QuestLogEntry:  [1010]
%qdt:
_qgiver_ de __qgiver_
dans la région de ___qgiver_
m'a envoyé faire une course pour
débarrasser _house_ dans __house_
d'un mort-vivant qui hante les lieux. J'ai
=traveltime_ jours pour accomplir cela.

Message:  1011
<ce>     Je suis l'ancêtre de celui qui a bâti cette maison.
<ce>       Elle appartient désormais au royaume des morts. Que
<ce>       toutes les créatures vivantes quittent cet endroit.

Message:  1012
<ce>        Je peux comprendre pourquoi vous n'êtes
<ce>        pas resté dans les parages pour combattre
<ce>        remerciements du temple pour avoir au moins
<ce>        découvert la raison pour laquelle les lieux
<ce>       étaient hantés. Voilà 2 pièces d'or.

-- Symbols used in the QRC file:
--
--              %pcn occurs 1 time.
--              %qdt occurs 1 time.
--      =traveltime_ occurs 2 times.
--        ___qgiver_ occurs 1 time.
--          __house_ occurs 3 times.
--         __qgiver_ occurs 1 time.
--            _gold_ occurs 2 times.
--           _house_ occurs 9 times.
--          _qgiver_ occurs 1 time.

QBN:
Item _gold_ gold
Item boobyprize gold range 2 to 2

Person _qgiver_ face 1 group Questor

Place _house_ remote house2

Clock _traveltime_ 00:00 0 flag 17 range 0 2

Foe _ghost_ is Ghost
Foe _skeleton_ is Skeletal_warrior
Foe _wraith_ is Wraith
Foe _zombie_ is Zombie

--	Quest start-up:
	start timer _traveltime_
	log 1010 step 0
	pick one of _S.02_ _S.03_ _S.04_ _S.05_
	pc at _house_ set _S.08_
	create npc at _house_

_traveltime_ task:
	end quest

_questdone_ task:
	clicked npc _qgiver_

_success_ task:
	when _questdone_ and _S.06_
	give pc _gold_
	end quest

_S.02_ task:
	place foe _ghost_ at _house_

_S.03_ task:
	place foe _skeleton_ at _house_

_S.04_ task:
	place foe _wraith_ at _house_

_S.05_ task:
	place foe _zombie_ at _house_

_S.06_ task:
	killed 1 _zombie_
	killed 1 _skeleton_
	killed 1 _wraith_
	killed 1 _ghost_

_S.07_ task:
	injured _zombie_ saying 1011
	injured _wraith_ saying 1011
	injured _skeleton_ saying 1011
	injured _ghost_ saying 1011

variable _S.08_
variable _S.09_
_clearclick_ task:
	when _questdone_ and not _S.06_
	clear _questdone_ _clearclick_
