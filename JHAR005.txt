Quest: JHAR005
DisplayName: Trésor Régional
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                    Nous avons une mission pour laquelle nous pensons que vous réussirez,
<ce>                    en supposant que vous n'êtes pas aussi frileux que certains de nos autres
<ce>                    membres. Il y a un objet enchanté dans ___dungeon_ que nous pensons être
<ce>                    d'origine brétonne, vieux d'au moins quelques siècles. On ne sait pas exactement
<ce>                    ce qu'il fait, ce qui comporte un certain degré de risque. Si je vous offrais
<ce>                    _reward_ pièces d'or, iriez-vous nous le récupérer ?
RefuseQuest:  [1001]
<ce>                    Lâche, comme tous les autres.
<--->
<ce>                    Vous ne monterez jamais en grade avec cette attitude.
<--->
<ce>                    Vraiment, %pcf.

AcceptQuest:  [1002]
<ce>               Alors peut-être que ce n'est pas une cause perdue après tout.
<ce>               J'attends votre retour avec impatience. Juste, s'il vous plaît,
<ce>               soyez prêt à tout. Il n'y a pas d'urgence dans cette affaire,
<ce>               alors ne saisissez l'objet que lorsque vous vous sentez prêt.
<ce>               J'espère le voir dans =traveltime_ jours. Prenez une charge
<ce>               de localisation. Cela vous aidera sans aucun doute cette fois.

QuestComplete:  [1004]
<ce>               Merci encore une fois, %pcf. Votre service est bien accueilli.

QuestorPostsuccess:  [1008]
J'espère que vous partagerez vos aventures avec les nouveaux membres, %pcf.

QuestLogEntry:  [1010]
%qdt:
 _questgiver_ des
  Archéologues m'ont envoyé à
 ___dungeon_ obtenir
  un _mitem_ enchanté.
 Je devrais être prêt à tout.
 Il doit arriver dans ___questgiver_
 dans =traveltime_ jours.

Message:  1040
<ce>           Vous inspectez _mitem_.
<ce>           Il a un symbole de soleil gravé.
<ce>           Vous semblez entendre un sifflement
<ce>           lorsque vous le tenez, ainsi que des
<ce>           chuchotements dans une langue inintelligible.

Message:  1041
<ce>           Vous inspectez _mitem_. Il a un symbole de lune
<ce>           gravé. Vous semblez entendre des hurlements
<ce>           lorsque vous le tenez, ainsi que des chuchotements
<ce>           dans une langue inintelligible.

Message:  1042
<ce>           Vous inspectez _mitem_.
<ce>           Il a un symbole de soleil gravé.
<ce>           Vous semblez entendre une respiration rauque
<ce>           lorsque vous le tenez, ainsi que des
<ce>           chuchotements dans une langue inintelligible.

Message:  1043
<ce>           Vous inspectez _mitem_. Il a un symbole de lune
<ce>           gravé. Vous semblez entendre des grincements
<ce>           lorsque vous le tenez, ainsi que des chuchotements
<ce>           dans une langue inintelligible.

Message:  1050
<ce>               Quelle joie de vous voir ! Ce petit objet est assez puissant.
<ce>               Je vais atténuer l'enchantement pour le moment afin que vous
<ce>               puissiez avoir la paix et tranquillité, mais je dirai
<ce>               simplement que cela vaut des années d'étude de textes. Il suffit
<ce>               de l'écouter attentivement. Vous apprendrez beaucoup. Si nous
<ce>               entendons parler d'un autre bijou similaire, j'espère que vous
<ce>               serez prêt à relever le défi. Voila votre or.

Message:  1051
<ce>               Quelle joie de vous voir ! Ce petit objet est assez puissant.
<ce>               Je vais atténuer l'enchantement pour le moment afin que vous
<ce>               puissiez avoir la paix et tranquillité, mais je dirai
<ce>               simplement que cela vaut des années d'étude de textes. Il suffit
<ce>               de l'écouter attentivement. Vous apprendrez beaucoup. Si nous
<ce>               entendons parler d'un autre bijou similaire, j'espère que vous
<ce>               serez prêt à relever le défi. Je voulais aussi mentionner que
<ce>               j'ai vos _reward_ pièces d'or ici, mais j'ai reçu ce _altrew_
<ce>               pendant votre absence. L'objet est enchanté, mais je ne sais pas
<ce>               avec quoi. Préférez-vous qu'il soit votre récompense au lieu de l'or ?

QBN:
Item _mitem_ trinket
Item _reward_ gold range 501 to 799
Item _altrew_ magic_item

Person _questgiver_ group Questor female

Place _dungeon_ remote dungeon

Clock _traveltime_ 00:00 0 flag 17 range 0 2

Foe _e1_ is Spider
Foe _e2_ is Werewolf
Foe _e2a_ is Wereboar
Foe _e3_ is Imp
Foe _e4_ is Giant_bat
Foe _e4a_ is Giant_rat

--	Quest start-up:
	place item _mitem_ at _dungeon_ 
	start timer _traveltime_ 
	reveal _dungeon_ 
	log 1010 step 0
                pick one of _b1_ _b2_ _b3_ _b4_
                pick one of _stand_ _alt_

variable _b1_
variable _b2_
variable _b3_
variable _b4_
variable _stand_
variable _alt_

_traveltime_ task:
	end quest 

_grabbed_ task:
               clicked item _mitem_

_spawn1_ task:
               when _grabbed_ and _b1_
               say 1040
               create foe _e1_ every 30 minutes indefinitely with 50% success

_spawn2_ task:
               when _grabbed_ and _b2_
               say 1041
               create foe _e2_ every 30 minutes indefinitely with 25% success
               create foe _e2a_ every 30 minutes indefinitely with 25% success

_spawn3_ task:
               when _grabbed_ and _b3_
               say 1042
               create foe _e3_ every 30 minutes indefinitely with 50% success

_spawn4_ task:
               when _grabbed_ and _b4_
               say 1043
               create foe _e4_ every 30 minutes indefinitely with 25% success
               create foe _e4a_ every 30 minutes indefinitely with 25% success

_S.01_ task:
	toting _mitem_ and _questgiver_ clicked

_normend_ task:
                when _S.01_ and _stand_
                say 1050
	give pc _reward_ 
	end quest 

_altend_ task:
                when _S.01_ and _alt_
                prompt 1051 yes _altend2_ no _simple_

_altend2_ task:
                give pc _altrew_
                end quest

_simple_ task:
                give pc _reward_
                end quest
