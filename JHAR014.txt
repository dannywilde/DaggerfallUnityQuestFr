Quest: JHAR014
DisplayName: Of Orcs and Men
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  There's a particular interest in the Orcish language
<ce>  of late, and good sums of money are waiting for those
<ce>  considered experts in it. Would you include yourself
<ce>  in that definition, and be willing to show it under
<ce>  mild levels of danger?

RefuseQuest:  [1001]
<ce>  The gold will reach worthy hands.

AcceptQuest:  [1002]
<ce>  Stellar. Take with you these two, who have paid
<ce>  good money to watch Orcish dialogue in action.
<ce>  _dummy1_ and _dummy2_, stay
<ce>  close to %pcn and keep out of
<ce>  danger. You are to travel to ___dung_,
<ce>  a known habitat for the savages. Please
<ce>  avoid any bloodshed as far as it is possible.
<ce>  The tribe living there has an understanding that
<ce>  the Archaeologists are friendly, and we put
<ce>  great trust in you to maintain that reputation.
<ce>  Your patrons have committed to paying you
<ce>  =reward_ gold when they are satisfied.

QuestFail:  [1003]
<ce>  On your way to ___dung_? As I thought.

QuestComplete:  [1004]
<ce>  Your spectators have decided they have seen enough.
<ce>  They decide to head back to ___qgiver_,
<ce>  though not before giving you the money.

Message:  1010
<ce>  You notice in the shadows a passive orc and
<ce>  strike up a cautious conversation. It speaks
<ce>  to you in return, familiar with the Archaeologist
<ce>  routine. The two of you commit to maintaining
<ce>  peace insofar as you are not attacked.

Message:  1011
<ce>  The injured orc bellows with rage.

Message:  1012
<ce>  This orc falls, and the sound of it echoes throughout
<ce>  the halls. Your welcome has worn out.

Message:  1013
<ce>  You notice in the shadows an orc and jabber at
<ce>  it with limited skill. You fail to communicate
<ce>  your peaceful purpose here, and he mistakes
<ce>  you for an enemy. Perhaps you are not the
<ce>  expert you claim to be.

Message:  1014
<ce>  Your sponsors grow agitated at the time you've
<ce>  spent on other matters than what they asked.
<ce>  They excuse themselves to go look elsewhere.

Message:  1015
<ce>  _dummy1_ asks, "What are
<ce>  we doing outside, pray tell?
<ce>  Are there more orcs out here?"

Message:  1030
%qdt:
 Through the Archaeologists
 of ___qgiver_, I have been
 hired to lead two spectators
 into ___dung_ to watch
 my skills with the Orcish language.
 I am to remain within until they
 are satisfied for =reward_ gold.

QBN:
Item _reward_ gold

Place _dung_ remote dungeon1

Person _qgiver_ group Questor
Person _dummy1_ group Resident1
Person _dummy2_ group Resident2

Clock _timer_ 00:00 0 flag 17 range 1 5
Clock _final_ 8:00 16:00
Clock _spawn_ 0:03 0:06

Foe _peace_ is Orc
Foe _fight_ is Orc
Foe _nme1_ is Orc_sergeant
Foe _nme2_ is Orc_warlord

--	Quest start-up:
    start timer _timer_
    add _dummy1_ face
    add _dummy2_ face
    pc at _dung_ do _inside_
    reveal _dung_
    log 1030 step 0

_inside_ task:
    start timer _final_
    stop timer _timer_
    start timer _spawn_

_notinside_ task:
    when not _inside_
    stop timer _final_
    start timer _timer_
    stop timer _spawn_

_skilled_ task:
    when skill Orcish is at least 20

variable _spawn_
_good_ task:
    when _skilled_ and _spawn_ and _inside_ and not _slain_
    create foe _peace_ every 0 minutes 1 times with 100% success
    say 1010
    restrain foe _peace_
    create foe _peace_ every 28 minutes indefinitely with 5% success

_inj_ task:
    injured _peace_
    say 1011

_advise_ task:
    when _spawn_ and not _inside_
    say 1015

_slain_ task:
    killed _peace_
    say 1012

_badspawn_ task:
    when _slain_ and _inside_
    create foe _fight_ every 28 minutes indefinitely with 5% success
    create foe _nme1_ every 65 minutes indefinitely with 5% success
    create foe _nme2_ every 87 minutes indefinitely with 3% success

_nogood_ task:
    when _spawn_ and _inside_ and not _skilled_
    say 1013
    create foe _fight_ every 0 minutes 1 times with 100% success
    create foe _fight_ every 28 minutes indefinitely with 5% success
    create foe _nme1_ every 65 minutes indefinitely with 5% success
    create foe _nme2_ every 87 minutes indefinitely with 3% success

_qgclicked_ task:
    clicked npc _qgiver_
    say 1003

_final_ task:
    give pc _reward_
    end quest

_timer_ task:
    say 1014
    end quest
