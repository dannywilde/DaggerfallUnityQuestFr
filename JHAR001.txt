Messages: 23
Quest: JHAR001
DisplayName: Notes empruntées
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                 Les archéologues ont une réputation
<ce>                 d'excellence, de connaissances et de précision
<ce>                 que nous attribuons en partie à des membres tels que vous,
<ce>                 et en partie à la possession des meilleures recherches.
<ce>                 Nous avons appris qu'un novice de la Guilde des Mages
<ce>                 _scholar_, est actuellement à ___dest_ avec
<ce>                 quelques notes que nous trouverions particulièrement
<ce>                 utiles en ce moment. Nous avons besoin de quelqu'un
<ce>                 pour aller les récupérer sous la bonne apparence.
<ce>                 Etes-vous capable de les récupérer, %pcf?
                                     <--->
<ce>                 Nous avons eu une relation un peu compliquée avec la Guilde
<ce>                 des Mages par le passé pour diverses raisons. 
<ce>                 Nous, comme beaucoup, détestons leur arrogance
<ce>                 et leur insistance à garder leur travail privé.
<ce>                 Nous pouvons remédier au second problème cette saison.
<ce>                 Un mage, _scholar_, séjourne à ___dest_ avec 
<ce>                 un ensemble de notes. Nous vous
<ce>                 demandons d'aller les récupérer pour nous.

RefuseQuest:  [1001]
<ce>                  Alors qu'est-ce que vous faites ici ? Shoo.
<--->
<ce>                  Bien, poussez-vous. Cela doit être fait par quelqu'un d'autre.
<--->
<ce>                  Alors, voudriez-vous partir, s'il vous plaît ?
<ce>                  Il y a du travail à faire ici.

AcceptQuest:  [1002]
<ce>                   Très bien. Et rappelez-vous, c'est dans ___dest_.
<ce>                   Maintenant, tout d'abord, ne faites aucune mention de votre identité.
<ce>                   Trouvez un moyen d'obtenir ces notes, de préférence sans
<ce>                   faire de vous un meurtrier. Nous n'avons pas besoin
<ce>                   d'une telle réputation dans cette guilde. Notre projet de recherche
<ce>                   parallèle sera lancé dans =queston_ jours, votre mission sera donc
<ce>                   un échec si vous n'êtes pas revenu à ce moment-là.
<ce>                   Soyez prudent, %pcf. Adieu.
<--->
<ce>                   Je ne sais pas combien de temps _scholar_ restera dans
<ce>                   ___dest_, donc le temps presse. J'aurai
<ce>                   _reward_ pièces d'or pour vous quand vous reviendrez.
<ce>                   S'il vous plaît, assurez-vous juste de ne pas laisser
<ce>                   derrière vous une réputation de meurtrier quand ce sera fini.
<ce>                   Reviens juste ici en un seul morceau sans ternir notre réputation
<ce>                   Nous en avons besoin dans pas plus de =queston_ jours.

QuestComplete:  [1004]
<ce>              Merveilleux, %pcf. J'espère simplement que vous avez couvert votre
<ce>              pistes avant de rentrer à ___qgiver_. Si la garde de la ville
<ce>              vient vous chercher, je ne vous couvrirai pas.
<ce>              Mais pour l'heure, voici votre récompense.


QuestLogEntry:  [1010]
%qdt:
 Les Archéologues de ___qgiver_
 m'ont demandé d'obtenir des notes de recherche
 de _scholar_, qui reste
 à ___dest_. Je devrais
 être de retour dans =queston_ jours.

Message:  1020
<ce>                Attendez, qui êtes-vous ? Mes notes de
<ce>                recherche ? Mais je viens de les finir.
<ce>                Est-ce que _dummy_ vous a envoyé
<--->
<ce>                Je n'ai aucune idée de qui vous êtes ni pourquoi
<ce>                vous demanderiez mes notes de recherche.
<ce>                Je les chéris plus que ma propre vie.
<ce>                Est-ce que _dummy_ vous a donné la permission
<ce>                demander cela ?

Message:  1021
<ce>                Eh bien, très bien alors. J'en aurai besoin d'ici
<ce>                _falsetimer_ jours. Si vous n'êtes revenu par
<ce>                contre j'irai à ___falsehouse_
<ce>                et vous devrez me retrouver là-bas.
<ce>                %oth, parfois la Guilde des Mages
<ce>                pose plus de problèmes que cela n'en vaut la peine.
<--->
<ce>                C'est facheux. Je ne peux pas prêter mes dernières
<ce>                notes de recherche à chaque %ra que la guilde m'envoie.
<ce>                J'ai besoin que tu me les rapportes à ___falsehouse_
<ce>                quand ils ont fini, et dites à _dummy_ que c'est
<ce>                la dernière fois que je m'embête avec ces absurdités.
Message:  1022
<ce>               Alors je te renverai d'où tu es venu !
<--->
<ce>               Partez, %ra ! Vous n'obtiendrez rien de moi !

Message:  1023
<ce>                Vous déséquilibrez _scholar_
<ce>                et arrachez rapidement le parchemin.
<ce>                Il est temps de rentrer à ___qgiver_.

Message:  1024
<ce>                _scholar_ gît, mort à vos pieds. Il ne fait aucun
<ce>                doute que la nouvelle circulera autour de %crn.

Message:  1030
<ce>               Vous essayez, essayez mais vous ne pouvez pas briser le sceau sur ce document.
<--->
<ce>               Ce parchemin est scellé hermétiquement.
<--->
<ce>               Le sceau sur ce document est insensible à vos tentatives.

Message:  1040
<ce>               C'est pathétiquement évident. je viens de parler à
<ce>               _dummy_ hier. Croyez-vous vraiment
<ce>               que je vais croire un mensonge aussi naïf ?
<ce>               Pourtant l'expérience me dit qu'un crétin comme toi
<ce>               va même tuer pour obtenir des notes triviales
<ce>               d'un innocent tel que moi. Je vous fais donc une
<ce>               proposition. Je ferai une série de fausses notes
<ce>               que vous pourrez apporter à votre employeur impie,
<ce>               et aucun sang ne sera versé aujourd'hui. N'est-ce pas
<ce>               une solution acceptable à notre dilemme, %ra?

Message:  1041
<ce>               En effet, l'honneur parmi les voleurs est un discours vide de sens.
<ce>               Je vous remercie néanmoins d'avoir l'esprit ouvert
<ce>               à ce sujet. Un autre mercenaire à tête épaisse
<ce>               serait plus heureux de me voir mort que de récupérer
<ce>               le document. Telle est la voie de la baie d'Iliaque de nos jours.
<ce>               
<ce>               Voilà, c'est fait. Maintenant, prenez-le et perdez-vous avant.
<ce>               J'appelle le garde et nous sautons tous ensemble en Oblivion.

Message:  1042
<ce>               Alors je le défendrai de ma vie ! A vous, sale bête !
<--->
<ce>               Alors mourez ! Pas de pitié pour les canailles criminelles !
<--->
<ce>               Alors il n'y a plus d'espoir pour vous ! Mourez !

Message:  1043
<ce>               Vous supposez que cela peut être considéré comme de la légitime défense.
<ce>               self-defense. Passons maintenant à la recherche.

QBN:
Item _realnotes_ letter used 1030
Item _falsenotes_ letter used 1030
Item _reward_ gold range 351 to 599
Item _falsetimer_ gold range 4 to 15

Person _qgiver_ group Questor male
Person _dummy_ group Local_3.0
Person _scholar_ group Spellcaster

Place _falsehouse_ remote tavern
Place _dest_ remote random

Foe _mage1_ is Mage
Foe _mage2_ is Battle_mage
Foe _e1_ is Imp
Foe _e2_ is Gargoyle
Foe _f1_ is Fire_atronach

Clock _queston_ 00:00 0 flag 17 range 1 2
Clock _spawn_ 2:00 1.00:00

--	Quest start-up:
	start timer _queston_ 
	log 1010 step 0 
	pick one of _true_ _false_
                place npc _scholar_ at _dest_

variable _true_
variable _false_

_queston_ task:
	end quest 

_talked_ task:
	clicked npc _scholar_ 

_path1_ task:
                when _talked_ and _true_
                prompt 1020 yes _path1a_ no _path1b_

_path1a_ task:
	get item _realnotes_
	say 1021

_path1b_ task:
                say 1022
                hide npc _scholar_
                place foe _mage1_ at _dest_
                create foe _f1_ every 1 minutes 1 times with 100% success

_steal_ task:
    injured _mage1_
    say 1023
	get item _realnotes_
                
_murder_ task:
    killed _mage1_
    say 1024
    change repute with _qgiver_ by -7

_path2_ task:
                when _talked_ and _false_
                prompt 1040 yes _path2a_ no _path2b_

_path2a_ task:
	get item _falsenotes_
	say 1041
                start timer _spawn_

_path2b_ task:
                say 1042
                hide npc _scholar_
                place foe _mage2_ at _dest_
                give item _realnotes_ to _mage2_

_intentional_ task:
    killed _mage2_
    say 1043

_spawn_ task:
    create foe _e1_ every 45 minutes 1 times with 35% success
    create foe _e2_ every 45 minutes 1 times with 35% success

_S.12_ task:
	toting _realnotes_ and _qgiver_ clicked 
	give pc _reward_ 
	end quest 

_S.13_ task:
	toting _falsenotes_ and _qgiver_ clicked 
	give pc _reward_ 
    change repute with _qgiver_ by -1
	end quest 
