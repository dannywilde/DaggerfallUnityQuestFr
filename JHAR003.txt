-- Archaeologist Quest
Quest: JHAR003
DisplayName: Squelettes dans le Placard
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>            Nous essayons d'éviter la politique contemporaine dans la mesure du possible,
<ce>            %pcf. La dernière chose dont nous avons besoin, c'est qu'un monarque
<ce>            fasse une crise de colère et décapite des officiels de la guilde
<ce>            simplement parce que quelqu'un dit quelque chose qui dérange
<ce>             la famille royale. C'est, je peux maintenant le dire, notre situation
<ce>            actuelle. Apparemment, un sorcier a identifié quelques os à
<ce>             ___dung_ qui semblent être les os d'un ancêtre perdu depuis longtemps.
<ce>            Les détails sont complexes et inintéressants, mais %crn souhaite
<ce>            garder les gens dans l'ignorance, et notre échec entraînera de graves
<ce>            conséquences. Serez-vous celui qui résoudra cette crise ?

RefuseQuest:  [1001]
<ce>         Pathétique.
<--->
<ce>         Je vois. Envisagez-vous de faire quoi que ce soit pour nous aider ?
<--->
<ce>         N'avons-nous pas d'importance pour vous, %pcf?

AcceptQuest:  [1002]
<ce>         N'oubliez pas qu'ils sont dans ___dung_.
<ce>         Selon la déclaration, les os étaient enveloppés dans une
<ce>         sorte de sac. Une présentation aussi inintéressante a
<ce>         repoussé les chasseurs de trésors jusqu'à présent, je suppose.
<ce>         Ne prenez pas plus de =timer_ jours pour le ramener ici.
<ce>         Notre réputation est en jeu, aussi injuste que cela puisse paraître.
<ce>         Des jours meilleurs sont devant nous, %pcf. Bonne chance.

QuestFail:  [1003]
<ce>         La date limite est passée pour ramener les anciens
<ce>         ossements de ___dung_. _qg_
<ce>         n'acceptera pas l'échec.

QuestComplete:  [1004]
<ce>         Merci encore une fois, %pcf. Puisse votre satisfaction
<ce>         pour un si bon travail l'emporter sur cette maigre récompense.

Message:  1020
<ce>         Excellent. Je vais envoyer ça tout de suite. Nous vous avons attribué
<ce>         _reward_ pièces d'or comme récompense.

Message:  1021
<ce>        Magnifique. Vous nous avez vraiment sauvés de certaines menaces
<ce>        assez crédibles. Nous vous avions attribué _reward_ pièces d'or,
<ce>        mais un membre a récemment remis un _magicitem_ magique à la guilde.
<ce>        Je n'ai pas encore pris la peine de l'identifier, si vous voulez bien
<ce>        m'excuser. Préférez-vous qu'il soit une récompense au lieu de l'or ?

Message:  1025
<ce>        Ce sac dégage un pouvoir magique subtil. Son seul effet évident
<ce>        est qu'il semble être bien entretenu. Celui qui a rassemblé ces
<ce>        ossements savait qu'ils auraient une histoire importante à raconter.

Message:  1030
%qdt:
 Les archéologues de ___qg_
 ont besoin de moi pour apporter des os de
 ___dung_ d'ici =timer_ jours.
 Ils devraient être dans un sac quelconque.

Message:  1040
<ce>        Dès que votre main touche le sac, vous savez que quelque
<ce>        chose ne va pas. Il est rempli de pierres, pas d'os.
<ce>        Vous ressentez également des mouvement à proximité.

Message:  1041
<ce>        Quel que soit le but du sorcier, son plan est à présent déjoué.
<ce>        Vous serez le dernier aventurier affecté par ce piège.

Message:  1042
<ce>        Oh, cela fera de %crn la risée de la baie
<ce>        d'Iliaque. Une bonne dose de chantage les
<ce>        gardera à distance dans cette affaire.
<ce>        Bravo, au nom des Archéologues !

Message:  1060
<ce>        Une fois que le guerrier squelette a reçu le coup, il cesse de bouger.
<ce>        Vous remarquez un morceau de parchemin suspendu à l'une de ses côtes.
<ce>        Vous l'enlevez et le mettez dans votre sac.

Message:  1061
 %pcn,
<ce>
 Le Roi des Vers vous salue, mon ami. J'espère vous trouver de bonne humeur,
 car je souhaite vous demander une faveur. Parmi vos possessions se trouvent
 les os d'un _ancestor_, qui n'a pas trouvé de fin au malheur et à la cruauté
 alors qu'il était encore en vie. Du moins, c'est ce que me dit l'esprit
 %g3. Si vous considérez que l'histoire de %g3 a plus de
 valeur que les querelles de monarques enfantins, et que vous souhaitez montrer
 votre dévotion aux ouvriers de la magie noire, apportez les restes
 de %g3 ici. Un _magicitem2_ enchanté vous attend.
<ce>
 Sincèrement vôtre,
 _necro_
 _far_, ___far_

Message:  1062
<ce>      Voici à quoi ressemblait le corps de %g3. Quel dommage.
<ce>      Cela m'en dit encore plus que %g1 ne pourrait le faire. Mais je
<ce>      ne te retiendrai pas. Prends la magicite m2_ je te l'ai promis et
<ce>      souvenez-vous : bien que les mortels oublient, les morts ne le feront pas.

QBN:

Person _qg_ group Questor male
Person _ancestor_ group Local_3.0
Person _necro_ group Spellcaster

Item _reward_ gold range 801 to 1499
Item _magicitem_ magic_item
Item _magicitem2_ magic_item
Item _sack_ large_sack
Item _falsesack_ large_sack
Item _letter_ letter used 1061

Place _far_ remote house1
Place _dung_ remote dungeon

Foe _wiz_ is Mage
Foe _sk_ is Skeletal_warrior

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _skel_ 8:00 1.00:00

--   Quest start-up:
   start timer _timer_
   log 1030 step 0
   pick one of _good_ _good_ _good_ _bad_
   pick one of _norm_ _norm_ _mag_
   pick one of _sent_ _peace_
   reveal _dung_
   place npc _necro_ at _far_

variable _norm_
variable _mag_
variable _peace_
variable _sent_

_good_ task:
   place item _sack_ at _dung_

_bad_ task:
  place item _falsesack_ at _dung_

_found_ task:
  clicked item _sack_
  say 1025

_n1_ task:
  when _found_ and _sent_
  start timer _skel_

_vict1_ task:
 toting _sack_ and _qg_ clicked

_vict1a_ task:
 when _vict1_ and _norm_
 say 1020
 give pc _reward_
 end quest

_vict1b_ task:
 when _vict1_ and _mag_
 prompt 1021 yes _vict1c_ no _vict1d_

_vict1c_ task:
 give pc _magicitem_
 end quest

_vict1d_ task:
 give pc _reward_
 end quest

_spawn_ task:
  clicked item _falsesack_
  say 1040
  create foe _wiz_ every 1 minutes 1 times with 100% success

_kill_ task:
  killed 1 _wiz_
  say 1041

_falsevict_ task:
  toting _falsesack_ and _qg_ clicked
  say 1042
  give pc _reward_
  end quest

_skel_ task:
  create foe _sk_ every 1 minutes 1 times with 100% success

_skel2_ task:
  injured _sk_
  say 1060
  restrain foe _sk_
  get item _letter_

_nvict_ task:
  toting _sack_ and _necro_ clicked
  say 1062
  get item _magicitem2_
  make _magicitem2_ permanent
  end quest

_timer_ task:
    say 1003
    end quest
